{ pkgs, config, lib, sys, ... }:

let
  sunshineExec = "${pkgs.nixgl.auto.nixGLDefault}/bin/nixGL ${pkgs.sunshine}/bin/sunshine";
  sunshineAutostartDesktop = pkgs.runCommand "sunshine-autostart-desktop" {} ''
    mkdir -p $out/share/applications
    cp ${../../files/remote/sunshine.desktop} $out/share/applications/sunshine.desktop
  '';
in
{
  home.packages = with pkgs; [
    sunshine
    nixgl.auto.nixGLDefault
  ];
  systemd.user.services.sunshine = {
    Unit = {
      Description = "Sunshine Game Stream Host";
      After = [ "graphical-session.target" ];
      Wants = [ "graphical-session.target" ];
    };

    Service = {
      ExecStart = sunshineExec;
      Environment = [
        "DISPLAY=:0"
        "XAUTHORITY=%h/.Xauthority"
      ];
      
      Restart = "always";
      RestartSec = 5;
    };

    Install = {
      WantedBy = [ "graphical-session.target" ];
    };
  };

  programs.gnome-shell = {
    enable = true;
    extensions = [
      { package = pkgs.gnomeExtensions.sunshinestatus; }
    ];
  };

  xdg.autostart.entries = [
    sunshineAutostartDesktop
  ];

  home.file.".mac_screen".text = ''
    add 2560 1664 60
    switch 2560x1664_60_User
    scale 200
    text-scale 1.1
  '';
  home.file.".no_screen".text = ''
    text-scale 1.0
  '';

  home.file.".screen".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/.mac_screen";

  home.activation.setupSunshineInput = sys.task.root {
    message = "Setting up Sunshine uinput permissions...";
    script = ''
      ${sys.deploy {
        name = "85-sunshine-input.rules";
        format = "lines";
        data = [
          ''KERNEL=="uinput", GROUP="input", MODE="0660", OPTIONS+="static_node=uinput"''
          ''KERNEL=="uhid", GROUP="input", MODE="0660", OPTIONS+="static_node=uhid"''
        ];
        target = "/etc/udev/rules.d/85-sunshine-input.rules";
        mode = "0644";
      }}

      ${sys.deploy {
        name = "uinput.conf";
        format = "lines";
        data = [ "uinput" "uhid" ];
        target = "/etc/modules-load.d/uinput.conf";
        mode = "0644";
      }}

      if ! id -nG "${config.home.username}" | grep -qw input; then
        esudo ${sys.cmds.usermod} -aG input ${config.home.username}
      fi

      esudo ${sys.cmds.modprobe} uinput
      esudo ${sys.cmds.modprobe} uhid
      esudo ${sys.cmds.udevadm} control --reload-rules
      esudo ${sys.cmds.udevadm} trigger

    '';
  };
}